!                 DATA_GRIB2_READ_ELABOR

! Procedure that reads input data in grib2 format,
! elaborates data, and writes output data in grib2 format

!-----------------------------------------------------------------------------
MODULE DATA_INPUT

! ------  INPUT DATA GRID PARAMETERS ------

! NX_INP, NY_INP : input data grid dimensions
! NLEV_P_INP : input data p-levels (isobaric levels) number
! NLEV_S_INP : input data soil levels number
! DX_INP, DY_INP: input data grid steps
! ALON_INP, ALAT0_INP: coord. in deg of the SW corner of the input data grid
! X0_INP, Y0_INP: coord. in deg of the rotation of the input data grid

  INTEGER :: NX_INP, NY_INP, NLEV_ATM_INP, NLEV_ATM_INP_MAX=100, NLEV_SOIL_INP, NLEV_SOIL_INP_MAX=20,&
             LEVEL_TYPE, FLAG_ROT, FLAG_CUT_PASTE
  REAL :: DX_INP, DY_INP, X0_INP, Y0_INP, ALON0_INP, ALAT0_INP

! Time parameters
! IDATE0: date and time (year, month, day, hours, minutes) of initial instant of forecast
! IDATEC: validity time of forecast
! IPERIOD: time intervals (days, hours, minutes)
! IPERIOD_ACCUM: time interval for accumulated precipitation

 INTEGER :: IDATE0(5), IDATEC(5), IPERIOD_INP(4), IPERIOD(3), IPERIOD_ACCUM(3)

! Atmospheric and Soil level (m) in input data

 REAL, DIMENSION(:), ALLOCATABLE :: LEV_LIST_ATM, LEV_LIST_SOIL

! Input fields and related parameters:

 INTEGER :: NPAR3D=20, NPAR2D=50, NPAR3D_SOIL=10

 REAL, DIMENSION(:,:,:,:), ALLOCATABLE :: FIELD3D, FIELD3D_SOIL
 REAL, DIMENSION(:,:,:), ALLOCATABLE   :: FIELD2D
 REAL, DIMENSION(:), ALLOCATABLE       :: ALEV, BLEV

 REAL, DIMENSION(:,:), ALLOCATABLE :: PREC_TOT, PREC_CON, PREC_SNO

 REAL :: VAL_MISSING=-9999.

 INTEGER :: ICENTRE, ISUBCENTRE, IMODEL

END MODULE DATA_INPUT
!-----------------------------------------------------------------------------

PROGRAM DATA_GRIB2_READ_WRITE

USE DATA_INPUT
USE GRIB2_CODING_DATA

IMPLICIT NONE

! Strings for input files names

CHARACTER*80 :: FILEGRIB='input_001.grib2'
CHARACTER (LEN=10) :: MODEL_NAME

INTEGER :: IFILE, IGRIB, I, J, NDAY, NDM, IIMON, ACCUM, IFORECAST_H, IACCUM_H, IFIELD
INTEGER, DIMENSION(12) ::IMON=(/31,28,31,30,31,30,31,31,30,31,30,31/)

! Reading of input data grid parameters only in input files

 CALL READ_GRIB2_DATA(1,FILEGRIB,1,.TRUE.,FLAG_CUT_PASTE,ICENTRE,ISUBCENTRE,IMODEL,                                    &
  NX_INP,NY_INP,NLEV_ATM_INP,NLEV_ATM_INP_MAX,NLEV_SOIL_INP,NLEV_SOIL_INP_MAX,                              &
  X0_INP,Y0_INP,ALON0_INP,ALAT0_INP,DX_INP,DY_INP,IDATE0,IPERIOD_INP,LEV_LIST_ATM,LEV_LIST_SOIL,LEVEL_TYPE, &
  NPAR3D,NPAR2D,NPAR3d_SOIL,FIELD3D,FIELD2D,FIELD3D_SOIL,ALEV,BLEV,VAL_MISSING)

 FLAG_ROT=0
 IF (ABS(X0_INP) > 1.E-2 .OR. ABS(Y0_INP) > 1.E-2) FLAG_ROT=1

 PRINT *,'Center ',ICENTRE,'(98 - ECMWF, 7 - NCEP, 80 - Rome RSMC)'
 IF (ICENTRE == 80) PRINT *,'   SubCenter ',ISUBCENTRE,' (102 - CNR-ISAC)'
 IF (ICENTRE == 80.AND.ISUBCENTRE == 102) &
 PRINT *,'Input data generated by model number ',IMODEL,'(1 - Bolam, 2 - Moloch, 3 - Globo)'
 PRINT *,'  '
 PRINT *,'Parameters of the input data grid are:'
 PRINT *,'NX=',NX_INP,' NY=',NY_INP
 PRINT *,'ALON0=',ALON0_INP,'ALAT0=',ALAT0_INP,' DX=',DX_INP,'DY=',DY_INP
 PRINT *,'Rotation: ',FLAG_ROT,'  X0=',X0_INP,'Y0=',Y0_INP
 PRINT *

 IF (IMODEL == 1) MODEL_NAME="bolam"
 IF (IMODEL == 2) MODEL_NAME="moloch"
 IF (IMODEL == 3) MODEL_NAME="globo"

!---------------------------------------------------------------------------

! ALLOCATION OF MATRIXES

 ALLOCATE(FIELD3D(NX_INP,NY_INP,NLEV_ATM_INP_MAX,NPAR3D))
 ALLOCATE(FIELD2D(NX_INP,NY_INP,NPAR2D))
 ALLOCATE(FIELD3D_SOIL(NX_INP,NY_INP,NLEV_SOIL_INP_MAX,NPAR3D_SOIL))
 ALLOCATE(LEV_LIST_ATM(NLEV_ATM_INP_MAX))
 ALLOCATE(LEV_LIST_SOIL(NLEV_SOIL_INP_MAX))
 ALLOCATE(ALEV(NLEV_ATM_INP_MAX+1))
 ALLOCATE(BLEV(NLEV_ATM_INP_MAX+1))

 ALLOCATE(PREC_TOT(NX_INP,NY_INP))
 ALLOCATE(PREC_CON(NX_INP,NY_INP))
 ALLOCATE(PREC_SNO(NX_INP,NY_INP))

!---------------------------------------
! Start of loop for processing of input data files
!---------------------------------------

! Osadki dlja nakaplivanija

 PREC_TOT(:,:)=0.
 PREC_CON(:,:)=0.
 PREC_SNO(:,:)=0.
 ACCUM=0

 IFILE=0
 DO WHILE (.TRUE.)

 IFILE=IFILE+1
 WRITE (FILEGRIB(7:9),'(I3.3)') IFILE
 OPEN (10,FILE=FILEGRIB,STATUS='OLD',ERR=10)
 GOTO 11
10 EXIT

11 CLOSE (10)

 CALL READ_GRIB2_DATA(1,FILEGRIB,1,.FALSE.,FLAG_CUT_PASTE,ICENTRE,ISUBCENTRE,IMODEL,                                   &
  NX_INP,NY_INP,NLEV_ATM_INP,NLEV_ATM_INP_MAX,NLEV_SOIL_INP,NLEV_SOIL_INP_MAX,                              &
  X0_INP,Y0_INP,ALON0_INP,ALAT0_INP,DX_INP,DY_INP,IDATE0,IPERIOD_INP,LEV_LIST_ATM,LEV_LIST_SOIL,LEVEL_TYPE, &
  NPAR3D,NPAR2D,NPAR3D_SOIL,FIELD3D,FIELD2D,FIELD3D_SOIL,ALEV,BLEV,VAL_MISSING)

! Forecast time and accumulation period

 IF (IPERIOD_INP(1)==1) THEN         ! Time unit is hour
   IPERIOD(1)=IPERIOD_INP(2)/24                             ! Days
   IPERIOD(2)=IPERIOD_INP(2)-IPERIOD(1)*24                  ! Hours
   IPERIOD(3)=0                                             ! Minutes
   IPERIOD_ACCUM(1)=IPERIOD_INP(3)/24                       ! Days
   IPERIOD_ACCUM(2)=IPERIOD_INP(3)-IPERIOD_ACCUM(1)*24      ! Hours
   IPERIOD_ACCUM(3)=0                                       ! Minutes
 ELSEIF (IPERIOD_INP(1)==0) THEN     ! Time unit is minute
   IPERIOD(1)=IPERIOD_INP(2)/24/60                          ! Days
   IPERIOD(2)=(IPERIOD_INP(2)-IPERIOD(1)*24*60)/60          ! Hours
   IPERIOD(3)=IPERIOD_INP(2)-IPERIOD(1)*24*60-IPERIOD(2)*60 ! Minutes
   IPERIOD_ACCUM(1)=IPERIOD_INP(3)/24/60                                      ! Days
   IPERIOD_ACCUM(2)=(IPERIOD_INP(3)-IPERIOD_ACCUM(1)*24*60)/60                ! Hours
   IPERIOD_ACCUM(3)=IPERIOD_INP(3)-IPERIOD_ACCUM(1)*24*60-IPERIOD_ACCUM(2)*60 ! Minutes
 ENDIF

! Date and time for current forecast instant

 IDATEC(:)=IDATE0(:)
 IDATEC(3)=IDATEC(3)+IPERIOD(1)
 IDATEC(4)=IDATEC(4)+IPERIOD(2)
 IDATEC(5)=IDATEC(5)+IPERIOD(3)
 IF (MOD(IDATEC(1),4)==0) THEN
   IMON(2)=29
 ELSE
   IMON(2)=28
 ENDIF
 NDAY=IPERIOD(1)
 NDM=0
 DO IIMON=1,50
   NDAY=NDAY-IMON(IDATEC(2))
   IF (NDAY<0) THEN
     EXIT
   ELSE
     NDM=NDM+IMON(IDATEC(2))
     IDATEC(2)=IDATEC(2)+1
     IF (IDATEC(2)>12) THEN
       IDATEC(2)=IDATEC(2)-12
       IDATEC(1)=IDATEC(1)+1
       IF (MOD(IDATEC(1),4)==0) THEN
         IMON(2)=29
       ELSE
         IMON(2)=28
       ENDIF
     ENDIF
   ENDIF
 ENDDO
 IDATEC(3)=IDATEC(3)-NDM
 IF (IDATEC(5)>=60) THEN
   IDATEC(5)=IDATEC(5)-60
   IDATEC(4)=IDATEC(4)+1
 ENDIF
 IF (IDATEC(4)>=24) THEN
   IDATEC(4)=IDATEC(4)-24
   IDATEC(3)=IDATEC(3)+1
 ENDIF
 IF (IDATEC(3)>IMON(IDATEC(2))) THEN
   IDATEC(3)=IDATEC(3)-IMON(IDATEC(2))
   IDATEC(2)=IDATEC(2)+1
   IF (IDATEC(2)>12) THEN
     IDATEC(2)=IDATEC(2)-12
     IDATEC(1)=IDATEC(1)+1
   ENDIF
 ENDIF

 PRINT *
 PRINT *,'Data and time parameters of the input data are:'
 PRINT *,'IDATE0         ',IDATE0(1:5)
 PRINT *,'Forecast period (day hour min) ',IPERIOD(1:3)
 PRINT *,'IDATE_CURRENT ',IDATEC(1:5)
 PRINT *,'Accumulation period (day hour min) ',IPERIOD_ACCUM(1:3)

 ACCUM=ACCUM+IPERIOD_INP(3)

 IF (ALL(INT(FIELD2D(:,:,6)) /= INT(VAL_MISSING))) THEN
   PREC_TOT(:,:)=PREC_TOT(:,:)+FIELD2D(:,:,6)
 ELSE
   PREC_TOT(:,:)=VAL_MISSING
   PRINT *,'Total precipitation input data not found'
 ENDIF

 IF (ALL(INT(FIELD2D(:,:,7)) /= INT(VAL_MISSING))) THEN
   PREC_CON(:,:)=PREC_CON(:,:)+FIELD2D(:,:,7)
 ELSE
   PREC_CON(:,:)=VAL_MISSING
   PRINT *,'Convective precipitation input data not found'
 ENDIF

 IF (ALL(INT(FIELD2D(:,:,8)) /= INT(VAL_MISSING))) THEN
   PREC_SNO(:,:)=PREC_SNO(:,:)+FIELD2D(:,:,8)
 ELSE
   PREC_SNO(:,:)=VAL_MISSING
   PRINT *,'Ice(snow) precipitation input data not found'
 ENDIF

 CLOSE (11)

! End of loop on all input files

 ENDDO

 IFILE=IFILE-1
 IF (IFILE>0) THEN

  IF (IPERIOD_INP(1)==1) THEN         ! Time unit is hour
    IPERIOD_ACCUM(1)=ACCUM/24                       ! Days
    IPERIOD_ACCUM(2)=ACCUM-IPERIOD_ACCUM(1)*24      ! Hours
    IPERIOD_ACCUM(3)=0                              ! Minutes
  ELSEIF (IPERIOD_INP(1)==0) THEN     ! Time unit is minute
    IPERIOD_ACCUM(1)=ACCUM/24/60                                      ! Days
    IPERIOD_ACCUM(2)=(ACCUM-IPERIOD_ACCUM(1)*24*60)/60                ! Hours
    IPERIOD_ACCUM(3)=ACCUM-IPERIOD_ACCUM(1)*24*60-IPERIOD_ACCUM(2)*60 ! Minutes
  ENDIF

  IACCUM_H=IPERIOD_ACCUM(1)*24+IPERIOD_ACCUM(2)

! Preparation of data for grib2 coding

! Name of ouput file

  WRITE (OUTPUT_FILE_NAME,'(2A,I4.4,3I2.2,A,I3.3,2I2.2,A,I3.3,A)') &
 TRIM(MODEL_NAME),"_",IDATE0(1:4),"_",IPERIOD(1:3),"_accum_",IACCUM_H,"h.grib2"

  NFIELD=0

  IF (ALL(INT(PREC_TOT(:,:)) /= INT(VALMISS))) NFIELD=NFIELD+1
  IF (ALL(INT(PREC_CON(:,:)) /= INT(VALMISS))) NFIELD=NFIELD+1
  IF (ALL(INT(PREC_SNO(:,:)) /= INT(VALMISS))) NFIELD=NFIELD+1

! Cleaning of eventual previous allocations

  DO IFIELD=1,NFIELD0
    IF (ALLOCATED(DATA(IFIELD) % FIELD) ) DEALLOCATE (DATA(IFIELD) % FIELD)
  ENDDO
  DO IFIELD=1,NFIELD
    DATA(IFIELD) % GRIB2_DESCRIPT(:) = IVALMISS1
    DATA(IFIELD) % GRIB2_DESCRIPT(11:14) = IVALMISS
  ENDDO

! Description of data for grib2 coding (INTEGER, DIMENSION(50) :: GRIB2_DESCRIPT=IVALMISS, see module_write_grib2_data.F90)

  DATA(1:NFIELD) % GRIB2_DESCRIPT(1) = IMODEL

! Status and type of data

  DATA(1:NFIELD) % GRIB2_DESCRIPT(6) = 0 ! operational products
  DATA(1:NFIELD) % GRIB2_DESCRIPT(7) = 1 ! forecast
  DATA(1:NFIELD) % GRIB2_DESCRIPT(30) = 0 ! bit-map absent

! Grid parameters

  DATA(1:NFIELD) % GRIB2_DESCRIPT(2) = 1 ! grid template index - horizontal grid 
  DATA(1:NFIELD) % NX = NX_INP
  DATA(1:NFIELD) % NY = NY_INP
  DATA(1:NFIELD) % X0 = X0_INP
  DATA(1:NFIELD) % Y0 = Y0_INP
  DATA(1:NFIELD) % DX = DX_INP
  DATA(1:NFIELD) % DY = DY_INP
  DATA(1:NFIELD) % X00 = ALON0_INP
  DATA(1:NFIELD) % Y00 = ALAT0_INP

! Initial date and time

  DO IFIELD=1,NFIELD

    DATA(IFIELD) % IDATE0(1:5) = IDATE0(1:5)

! Date and time for current forecast term

    DATA(IFIELD) % IDATEC(1:5) = IDATEC(1:5)

  ENDDO

! Definition of time unit, forecast and period length in defined time unit

  DATA(1:NFIELD) % GRIB2_DESCRIPT(4) = IPERIOD_INP(1) ! Time unit for Forecast time step and Statistical period : 0 - minute, 1 -hour, 2 - day
  IF (IPERIOD_INP(1) == 1) THEN ! hour
    DATA(1:NFIELD) % IFORECAST = IPERIOD(1)*24+IPERIOD(2) ! Forecast time step
    DATA(1:NFIELD) % IPERIOD = IPERIOD_ACCUM(1)*24+IPERIOD_ACCUM(2) ! Statistical period
  ELSEIF (IPERIOD_INP(1) == 0) THEN ! minute
    DATA(1:NFIELD) % IFORECAST = IPERIOD(1)*1440+IPERIOD(2)*60+IPERIOD(3) ! Forecast time step
    DATA(1:NFIELD) % IPERIOD = IPERIOD_ACCUM(1)*1440+IPERIOD_ACCUM(2)*60+IPERIOD_ACCUM(3) ! Statistical period
  ELSEIF (IPERIOD_INP(1) == 2) THEN ! day
    DATA(1:NFIELD) % IFORECAST = IPERIOD(1) ! Forecast time step
    DATA(1:NFIELD) % IPERIOD = IPERIOD_ACCUM(1) ! Statistical period
  ELSE
    DATA(1:NFIELD) % IFORECAST = IVALMISS1 ! Forecast time step
    DATA(1:NFIELD) % IPERIOD = IVALMISS1 ! Statistical period
  ENDIF

! Product template index

  DATA(1:NFIELD) % GRIB2_DESCRIPT(3) = 8 ! Product template: Statistical
  DATA(1:NFIELD) % GRIB2_DESCRIPT(5) = 1 ! Statistical elaboration type: accumulation

! Level/layer parameters (for forecast satellite products parameters of satellite platform and sensor)

  DO IFIELD=1,NFIELD
    DATA(IFIELD) % GRIB2_DESCRIPT(10:15) = IVALMISS
  ENDDO

  DO IFIELD=1,NFIELD
    ALLOCATE(DATA(IFIELD) % FIELD(NX_INP,NY_INP))
  ENDDO

! Definition of data fields and data parameters in grib2 terms

  DATA(1:NFIELD) % GRIB2_DESCRIPT(10) = 1 ! Ground or water surface

! Defintion of product discipline

  DATA(1:NFIELD) % GRIB2_DESCRIPT(20) = 0 ! Discipline: Meteorological products

! Definition of category and parameter of products, and values of product

  IFIELD=0

! Total precipitation

  IF (ALL(INT(PREC_TOT(:,:)) /= INT(VALMISS))) THEN

    IFIELD=IFIELD+1

    DATA(IFIELD) % GRIB2_DESCRIPT(21) = 1 ! Category: Moisture
    DATA(IFIELD) % GRIB2_DESCRIPT(22) = 8 ! Parameter: Total precipitation (kg m-2)
    DATA(IFIELD) % FIELD(:,:) = PREC_TOT(:,:)

  ENDIF

! Convective precipitation

  IF (ALL(INT(PREC_CON(:,:)) /= INT(VALMISS))) THEN

    IFIELD=IFIELD+1

    DATA(IFIELD) % GRIB2_DESCRIPT(21) = 1 ! Category: Moisture
    DATA(IFIELD) % GRIB2_DESCRIPT(22) = 10 ! Parameter: Convective precipitation (kg m-2)

    DATA(IFIELD) % FIELD(:,:) = PREC_CON(:,:)

  ENDIF

! Solid phase precipitation

  IF (ALL(INT(PREC_SNO(:,:)) /= INT(VALMISS))) THEN

    IFIELD=IFIELD+1

    DATA(IFIELD) % GRIB2_DESCRIPT(21) = 1 ! Category: Moisture
    DATA(IFIELD) % GRIB2_DESCRIPT(22) = 29 ! Parameter: Total snowfall (m)
    DATA(IFIELD) % FIELD(:,:) = PREC_SNO(:,:)*1.E-3 ! mm ---> m

  ENDIF

! Finish of praparation of output data for coding in grib2 format

! Write output data in grib2 format

  CALL WRITE_GRIB2_DATA

 ELSE

  PRINT *,'Input data not read'

 ENDIF ! IFILE>0

STOP
END

!------------------------------------------------------------------------------
